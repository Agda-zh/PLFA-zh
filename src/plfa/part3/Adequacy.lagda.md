---
title     : "Adequacy: æŒ‡ç§°è¯­ä¹‰ç›¸å¯¹äºæ“ä½œè¯­ä¹‰çš„å……åˆ†æ€§"
permalink : /Adequacy/
translators : ["OlingCat"]
---

```agda
module plfa.part3.Adequacy where
```

<!--
## Introduction
-->

## å¼•è¨€

<!--
Having proved a preservation property in the last chapter, a natural
next step would be to prove progress. That is, to prove a property
of the form
-->

åœ¨ä¸Šä¸€ç« ä¸­è¯æ˜äº†ä¿å‹æ€§åï¼Œæ¥ä¸‹æ¥è‡ªç„¶å°±æ˜¯è¯æ˜å¯è¿›æ€§äº†ï¼Œå³è¯æ˜ä»¥ä¸‹æ€§è´¨ï¼š

<!--
    If Î³ âŠ¢ M â†“ v, then either M is a lambda abstraction or M â€”â†’ N for some N.
-->

    è‹¥ `Î³ âŠ¢ M â†“ v`ï¼Œé‚£ä¹ˆå¯¹äºæŸä¸ª `M`ï¼Œè¦ä¹ˆ `M` æ˜¯ä¸€ä¸ªÎ»-æŠ½è±¡ï¼Œè¦ä¹ˆ `M â€”â†’ N`ã€‚

<!--
Such a property would tell us that having a denotation implies either
reduction to normal form or divergence. This is indeed true, but we
can prove a much stronger property! In fact, having a denotation that
is a function value (not `âŠ¥`) implies reduction to a lambda
abstraction.
-->

è¿™æ ·çš„æ€§è´¨å‘Šè¯‰æˆ‘ä»¬ï¼Œæ‹¥æœ‰ä¸€ä¸ªæŒ‡ç§°è•´å«äº†è¦ä¹ˆå¯å½’çº¦ä¸ºæ­£è§„å½¢å¼ï¼Œè¦ä¹ˆå‘æ•£ã€‚
è™½ç„¶ç¡®å®å¦‚æ­¤ï¼Œä½†æ˜¯æˆ‘ä»¬å¯ä»¥è¯æ˜ä¸€ä¸ªæ›´å¼ºçš„æ€§è´¨ï¼äº‹å®ä¸Šï¼Œæ‹¥æœ‰ä¸€ä¸ªå‡½æ•°å€¼ï¼ˆé
`âŠ¥`ï¼‰çš„æŒ‡ç§°è•´å«äº†å®ƒå¯è§„çº¦ä¸º Î»-æŠ½è±¡ã€‚

<!--
This stronger property, reformulated a bit, is known as _adequacy_.
That is, if a term `M` is denotationally equal to a lambda abstraction,
then `M` reduces to a lambda abstraction.
-->

è¿™ç§æ›´å¼ºçš„å±æ€§å¯é‡æ–°è¡¨è¿°ä¸º**å……åˆ†æ€§ï¼ˆAdequacyï¼‰**ã€‚
ä¹Ÿå°±æ˜¯è¯´ï¼Œå¦‚æœé¡¹ `M` æŒ‡ç§°ç­‰ä»·äºä¸€ä¸ª Î»-æŠ½è±¡ï¼Œé‚£ä¹ˆ `M` å°±èƒ½è§„çº¦ä¸ºè¯¥ Î»-æŠ½è±¡ã€‚

<!--
    â„° M â‰ƒ â„° (Æ› N)  implies M â€”â†  Æ› N' for some N'
-->

    å¯¹äºæŸä¸ª N' è€Œè¨€ï¼Œâ„° M â‰ƒ â„° (Æ› N)  è•´å« M â€”â†  Æ› N'

<!--
Recall that `â„° M â‰ƒ â„° (Æ› N)` is equivalent to saying that `Î³ âŠ¢ M â†“ (v â†¦
w)` for some `v` and `w`. We will show that `Î³ âŠ¢ M â†“ (v â†¦ w)` implies
multi-step reduction a lambda abstraction.  The recursive structure of
the derivations for `Î³ âŠ¢ M â†“ (v â†¦ w)` are completely different from
the structure of multi-step reductions, so a direct proof would be
challenging. However, The structure of `Î³ âŠ¢ M â†“ (v â†¦ w)` closer to
that of [BigStep](/BigStep/) call-by-name
evaluation. Further, we already proved that big-step evaluation
implies multi-step reduction to a lambda (`cbnâ†’reduce`). So we shall
prove that `Î³ âŠ¢ M â†“ (v â†¦ w)` implies that `Î³' âŠ¢ M â‡“ c`, where `c` is a
closure (a term paired with an environment), `Î³'` is an environment
that maps variables to closures, and `Î³` and `Î³'` are appropriate
related.  The proof will be an induction on the derivation of
`Î³ âŠ¢ M â†“ v`, and to strengthen the induction hypothesis, we will relate
semantic values to closures using a _logical relation_ `ğ•`.
-->

å›æƒ³ä¸€ä¸‹ï¼Œå¯¹äºæŸäº› `v` å’Œ `w`ï¼Œ`â„° M â‰ƒ â„° (Æ› N)` ç­‰ä»·äº `Î³ âŠ¢ M â†“ (v â†¦ w)`ã€‚
æˆ‘ä»¬å°†è¯æ˜ `Î³ âŠ¢ M â†“ (v â†¦ w)` è•´å«äº† Î»-æŠ½è±¡çš„å¤šæ­¥è§„çº¦ã€‚`Î³ âŠ¢ M â†“ (v â†¦ w)`
çš„æ¨å¯¼è¿‡ç¨‹çš„é€’å½’ç»“æ„ä¸å¤šæ­¥å½’çº¦çš„ç»“æ„å®Œå…¨ä¸åŒï¼Œæ‰€ä»¥ç›´æ¥è¯æ˜æ˜¯å¾ˆå›°éš¾çš„ã€‚
ç„¶è€Œï¼Œ`Î³ âŠ¢ M â†“ (v â†¦ w)` çš„ç»“æ„æ›´æ¥è¿‘[å¤§æ­¥](/BigStep/)çš„ä¼ åæ±‚å€¼ã€‚
æ­¤å¤–ï¼Œæˆ‘ä»¬å·²ç»è¯æ˜å¤§æ­¥æ±‚å€¼æ„å‘³ç€å¤šæ­¥è§„çº¦ä¸º Î»ï¼ˆ`cbnâ†’reduce`ï¼‰ã€‚
æ‰€ä»¥æˆ‘ä»¬è¦è¯æ˜ `Î³ âŠ¢ M â†“ (v â†¦ w)` è•´å« `Î³' âŠ¢ M â‡“ c`ï¼Œå…¶ä¸­ `c` æ˜¯ä¸€ä¸ªé—­åŒ…
ï¼ˆä¸€ä¸ªé¡¹ä¸ç¯å¢ƒçš„åºå¯¹ï¼‰ï¼Œ`Î³'` æ˜¯ä¸€ä¸ªç¯å¢ƒï¼Œå®ƒå°†å˜é‡æ˜ å°„ä¸ºé—­åŒ…ï¼Œå¹¶ä¸” `Î³` å’Œ
`Î³'` ä»¥é€‚å½“çš„æ–¹å¼ç›¸å…³è”ã€‚è¯æ˜è¿‡ç¨‹æ˜¯å¯¹ `Î³ âŠ¢ M â†“ v` çš„æ¨å¯¼è¿‡ç¨‹çš„å½’çº³ï¼Œ
ä¸ºäº†åŠ å¼ºå½’çº³å‡è®¾ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨ **é€»è¾‘å…³ç³»ï¼ˆLogical Relationï¼‰** `ğ•`
å°†è¯­ä¹‰å€¼ä¸é—­åŒ…å…³è”èµ·æ¥ã€‚

<!--
The rest of this chapter is organized as follows.

* To make the `ğ•` relation down-closed with respect to `âŠ‘`,
  we must loosen the requirement that `M` result in a function value and
  instead require that `M` result in a value that is greater than or
  equal to a function value. We establish several properties about
  being ``greater than a function''.

* We define the logical relation `ğ•` that relates values and closures,
  and extend it to a relation on terms `ğ”¼` and environments `ğ”¾`.
  We prove several lemmas that culminate in the property that
  if `ğ• v c` and `vâ€² âŠ‘ v`, then `ğ• vâ€² c`.

* We prove the main lemma,
  that if `ğ”¾ Î³ Î³'` and `Î³ âŠ¢ M â†“ v`, then `ğ”¼ v (clos M Î³')`.

* We prove adequacy as a corollary to the main lemma.
-->

æœ¬ç« åé¢å†…å®¹çš„ç»“æ„ç»„ç»‡å¦‚ä¸‹ï¼š

* ä¸ºäº†ä½¿å…³ç³» `ğ•` ç›¸å¯¹äº `âŠ‘` å‘ä¸‹å°é—­ï¼Œæˆ‘ä»¬å¿…é¡»æ”¾å®½ `M` çš„ç»“æœå¿…é¡»ä¸ºå‡½æ•°å€¼çš„è¦æ±‚ï¼Œ
  è½¬è€Œè¦æ±‚ `M` çš„ç»“æœå¤§äºæˆ–ç­‰äºä¸€ä¸ªå‡½æ•°å€¼ã€‚æˆ‘ä»¬å»ºç«‹äº†å‡ ä¸ªå…³äºâ€œå¤§äºä¸€ä¸ªå‡½æ•°â€çš„æ€§è´¨ã€‚

* æˆ‘ä»¬å®šä¹‰äº†é€»è¾‘å…³ç³» `ğ•` å°†å€¼å’Œé—­åŒ…å…³è”äº†èµ·æ¥ï¼Œå¹¶å°†å®ƒæ‰©å±•ä¸ºé¡¹ `ğ”¼`
  å’Œç¯å¢ƒ `ğ”¾` ä¹‹é—´çš„å…³ç³»ã€‚æˆ‘ä»¬è¯æ˜äº†å‡ ä¸ªå¼•ç†ï¼Œæœ€ç»ˆå¾—å‡ºä»¥ä¸‹æ€§è´¨ï¼šè‹¥ `ğ• v c`
  ä¸” `vâ€² âŠ‘ v`ï¼Œåˆ™ `ğ• vâ€² c`ã€‚

* æˆ‘ä»¬è¯æ˜äº†ä¸»å¼•ç†ï¼šè‹¥ `ğ”¾ Î³ Î³'` ä¸” `Î³ âŠ¢ M â†“ v`ï¼Œåˆ™ `ğ”¼ v (clos M Î³')`ã€‚

* æˆ‘ä»¬è¯æ˜äº†ä¸»å¼•ç†çš„æ¨è®ºçš„å……åˆ†æ€§ã€‚


<!--
## Imports
-->

## å¯¼å…¥

```agda
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; _â‰¢_; refl; trans; sym; cong; congâ‚‚; cong-app)
open import Data.Product using (_Ã—_; Î£; Î£-syntax; âˆƒ; âˆƒ-syntax; projâ‚; projâ‚‚)
  renaming (_,_ to âŸ¨_,_âŸ©)
open import Data.Sum
open import Relation.Nullary using (Â¬_)
open import Relation.Nullary.Negation using (contradiction)
open import Data.Empty using (âŠ¥-elim) renaming (âŠ¥ to Bot)
open import Data.Unit
open import Relation.Nullary using (Dec; yes; no)
open import Function using (_âˆ˜_)
open import plfa.part2.Untyped
     using (Context; _âŠ¢_; â˜…; _âˆ‹_; âˆ…; _,_; Z; S_; `_; Æ›_; _Â·_;
            rename; subst; ext; exts; _[_]; subst-zero;
            _â€”â† _; _â€”â†’âŸ¨_âŸ©_; _âˆ; _â€”â†’_; Î¾â‚; Î¾â‚‚; Î²; Î¶)
open import plfa.part2.Substitution using (ids; sub-id)
open import plfa.part2.BigStep
     using (Clos; clos; ClosEnv; âˆ…'; _,'_; _âŠ¢_â‡“_; â‡“-var; â‡“-lam; â‡“-app;
            â‡“-determ; cbnâ†’reduce)
open import plfa.part3.Denotational
     using (Value; Env; `âˆ…; _`,_; _â†¦_; _âŠ‘_; _âŠ¢_â†“_; âŠ¥; all-funsâˆˆ; _âŠ”_; âˆˆâ†’âŠ‘;
            var; â†¦-elim; â†¦-intro; âŠ”-intro; âŠ¥-intro; sub; â„°; _â‰ƒ_; _iff_;
            âŠ‘-trans; âŠ‘-conj-R1; âŠ‘-conj-R2; âŠ‘-conj-L; âŠ‘-refl; âŠ‘-fun; âŠ‘-bot;
            âŠ‘-dist; sub-inv-fun)
open import plfa.part3.Soundness using (soundness)

```


<!--
## The property of being greater or equal to a function
-->

## å¤§äºæˆ–ç­‰äºä¸€ä¸ªå‡½æ•°çš„æ€§è´¨

<!--
We define the following short-hand for saying that a value is
greater-than or equal to a function value.
-->

æˆ‘ä»¬å®šä¹‰ä»¥ä¸‹ç®€å†™æ¥è¡¨ç¤ºä¸€ä¸ªå€¼å¤§äºæˆ–ç­‰äºä¸€ä¸ªå‡½æ•°å€¼ï¼š

```agda
above-fun : Value â†’ Set
above-fun u = Î£[ v âˆˆ Value ] Î£[ w âˆˆ Value ] v â†¦ w âŠ‘ u
```

<!--
If a value `u` is greater than a function, then an even greater value `u'`
is too.
-->

å¦‚æœå€¼ `u` å¤§äºä¸€ä¸ªå‡½æ•°ï¼Œé‚£ä¹ˆæ›´å¤§çš„å€¼ `u'` ä¹Ÿå¤§äºè¯¥å‡½æ•°ï¼š

```agda
above-fun-âŠ‘ : âˆ€{u u' : Value}
      â†’ above-fun u â†’ u âŠ‘ u'
        -------------------
      â†’ above-fun u'
above-fun-âŠ‘ âŸ¨ v , âŸ¨ w , lt' âŸ© âŸ© lt = âŸ¨ v , âŸ¨ w , âŠ‘-trans lt' lt âŸ© âŸ©
```

<!--
The bottom value `âŠ¥` is not greater than a function.
-->

åº•å€¼ `âŠ¥` ä¸å¤§äºä»»ä½•ä¸€ä¸ªå‡½æ•°ï¼š

```agda
above-funâŠ¥ : Â¬ above-fun âŠ¥
above-funâŠ¥ âŸ¨ v , âŸ¨ w , lt âŸ© âŸ©
    with sub-inv-fun lt
... | âŸ¨ Î“ , âŸ¨ f , âŸ¨ Î“âŠ†âŠ¥ , âŸ¨ lt1 , lt2 âŸ© âŸ© âŸ© âŸ©
    with all-funsâˆˆ f
... | âŸ¨ A , âŸ¨ B , m âŸ© âŸ©
    with Î“âŠ†âŠ¥ m
... | ()
```

<!--
If the join of two values `u` and `u'` is greater than a function, then
at least one of them is too.
-->

è‹¥ä¸¤ä¸ªå€¼ `u` å’Œ `u'` çš„è¿æ¥å¤§äºä¸€ä¸ªå‡½æ•°ï¼Œé‚£ä¹ˆè‡³å°‘å…¶ä¸­ä¹‹ä¸€å¤§äºè¯¥å‡½æ•°ï¼š

```agda
above-fun-âŠ” : âˆ€{u u'}
           â†’ above-fun (u âŠ” u')
           â†’ above-fun u âŠ above-fun u'
above-fun-âŠ”{u}{u'} âŸ¨ v , âŸ¨ w , vâ†¦wâŠ‘uâŠ”u' âŸ© âŸ©
    with sub-inv-fun vâ†¦wâŠ‘uâŠ”u'
... | âŸ¨ Î“ , âŸ¨ f , âŸ¨ Î“âŠ†uâŠ”u' , âŸ¨ lt1 , lt2 âŸ© âŸ© âŸ© âŸ©
    with all-funsâˆˆ f
... | âŸ¨ A , âŸ¨ B , m âŸ© âŸ©
    with Î“âŠ†uâŠ”u' m
... | injâ‚ x = injâ‚ âŸ¨ A , âŸ¨ B , (âˆˆâ†’âŠ‘ x) âŸ© âŸ©
... | injâ‚‚ x = injâ‚‚ âŸ¨ A , âŸ¨ B , (âˆˆâ†’âŠ‘ x) âŸ© âŸ©
```

<!--
On the other hand, if neither of `u` and `u'` is greater than a function,
then their join is also not greater than a function.
-->

å¦ä¸€æ–¹é¢ï¼Œè‹¥ `u` å’Œ `u'` éƒ½ä¸å¤§äºæŸä¸€ä¸ªå‡½æ•°ï¼Œé‚£ä¹ˆå®ƒä»¬çš„è¿æ¥éƒ½ä¸å¤§äºè¯¥å‡½æ•°ï¼š

```agda
not-above-fun-âŠ” : âˆ€{u u' : Value}
               â†’ Â¬ above-fun u â†’ Â¬ above-fun u'
               â†’ Â¬ above-fun (u âŠ” u')
not-above-fun-âŠ” naf1 naf2 af12
    with above-fun-âŠ” af12
... | injâ‚ af1 = contradiction af1 naf1
... | injâ‚‚ af2 = contradiction af2 naf2
```

<!--
The converse is also true. If the join of two values is not above a
function, then neither of them is individually.
-->

åä¹‹äº¦ç„¶ã€‚å¦‚æœä¸¤ä¸ªå€¼çš„è¿æ¥ä¸éƒ½ä¸å¤§äºæŸä¸ªå‡½æ•°ï¼Œé‚£ä¹ˆå®ƒä»¬å„è‡ªéƒ½ä¸å¤§äºè¯¥å‡½æ•°ã€‚

```agda
not-above-fun-âŠ”-inv : âˆ€{u u' : Value} â†’ Â¬ above-fun (u âŠ” u')
              â†’ Â¬ above-fun u Ã— Â¬ above-fun u'
not-above-fun-âŠ”-inv af = âŸ¨ f af , g af âŸ©
  where
    f : âˆ€{u u' : Value} â†’ Â¬ above-fun (u âŠ” u') â†’ Â¬ above-fun u
    f{u}{u'} af12 âŸ¨ v , âŸ¨ w , lt âŸ© âŸ© =
        contradiction âŸ¨ v , âŸ¨ w , âŠ‘-conj-R1 lt âŸ© âŸ© af12
    g : âˆ€{u u' : Value} â†’ Â¬ above-fun (u âŠ” u') â†’ Â¬ above-fun u'
    g{u}{u'} af12 âŸ¨ v , âŸ¨ w , lt âŸ© âŸ© =
        contradiction âŸ¨ v , âŸ¨ w , âŠ‘-conj-R2 lt âŸ© âŸ© af12
```

<!--
The property of being greater than a function value is decidable, as
exhibited by the following function.
-->

ã€Œå¤§äºä¸€ä¸ªå‡½æ•°å€¼ã€çš„æ€§è´¨æ˜¯å¯åˆ¤å®šçš„ï¼Œå¦‚ä»¥ä¸‹å‡½æ•°æ‰€ç¤ºï¼š

```agda
above-fun? : (v : Value) â†’ Dec (above-fun v)
above-fun? âŠ¥ = no above-funâŠ¥
above-fun? (v â†¦ w) = yes âŸ¨ v , âŸ¨ w , âŠ‘-refl âŸ© âŸ©
above-fun? (u âŠ” u')
    with above-fun? u | above-fun? u'
... | yes âŸ¨ v , âŸ¨ w , lt âŸ© âŸ© | _ = yes âŸ¨ v , âŸ¨ w , (âŠ‘-conj-R1 lt) âŸ© âŸ©
... | no _ | yes âŸ¨ v , âŸ¨ w , lt âŸ© âŸ© = yes âŸ¨ v , âŸ¨ w , (âŠ‘-conj-R2 lt) âŸ© âŸ©
... | no x | no y = no (not-above-fun-âŠ” x y)
```


<!--
## Relating values to closures
-->

## å°†å€¼å…³è”åˆ°é—­åŒ…

<!--
Next we relate semantic values to closures.  The relation `ğ•` is for
closures whose term is a lambda abstraction, i.e., in weak-head normal
form (WHNF). The relation ğ”¼ is for any closure. Roughly speaking,
`ğ”¼ v c` will hold if, when `v` is greater than a function value, `c` evaluates
to a closure `c'` in WHNF and `ğ• v c'`. Regarding `ğ• v c`, it will hold when
`c` is in WHNF, and if `v` is a function, the body of `c` evaluates
according to `v`.
-->

æ¥ä¸‹æ¥æˆ‘ä»¬å°†è¯­ä¹‰å€¼å…³è”è‡³é—­åŒ…ã€‚å…³ç³» `ğ•` åº”ç”¨äºé¡¹æ˜¯ Î»-æŠ½è±¡çš„é—­åŒ…ï¼Œå³
**å¼±å¤´è§„èŒƒå½¢å¼ï¼ˆweak-head normal formï¼Œç¼©å†™ WHNFï¼‰**ã€‚å…³ç³» `ğ”¼` åº”ç”¨äºä»»æ„é—­åŒ…ã€‚
å¤§è‡´æ¥è¯´ï¼Œå½“ `v` å¤§äºä¸€ä¸ªå‡½æ•°å€¼ï¼Œ`c` åœ¨ WHNF ä¸­æ±‚å€¼ä¸ºé—­åŒ… `c'`ï¼Œä¸”
`ğ• v c'` æ—¶ï¼Œ`ğ”¼ v c` æˆç«‹ã€‚å¯¹äº `ğ• v c` è€Œè¨€ï¼Œå®ƒåœ¨ `c` ä½äº WHNF ä¸­æ—¶æˆç«‹ï¼Œ
ä¸”è‹¥ `v` æ˜¯ä¸€ä¸ªå‡½æ•°ï¼Œåˆ™ `c` çš„ä¸»ä½“æ ¹æ® `v` è¿›è¡Œæ±‚å€¼ã€‚

```agda
ğ• : Value â†’ Clos â†’ Set
ğ”¼ : Value â†’ Clos â†’ Set
```

<!--
We define `ğ•` as a function from values and closures to `Set` and not as a
data type because it is mutually recursive with `ğ”¼` in a negative
position (to the left of an implication).  We first perform case
analysis on the term in the closure. If the term is a variable or
application, then `ğ•` is false (`Bot`). If the term is a lambda
abstraction, we define `ğ•` by recursion on the value, which we
describe below.
-->

æˆ‘ä»¬å°† `ğ•` å®šä¹‰ä¸ºä¸€ä¸ªä»å€¼å’Œé—­åŒ…åˆ° `Set` çš„å‡½æ•°ï¼Œè€Œéæ•°æ®ç±»å‹ï¼Œ
å› ä¸ºå®ƒåœ¨å¦å®šçš„ä½ç½®ï¼ˆè•´å«å¼çš„å·¦ä¾§ï¼‰ä¸ `ğ”¼` äº’é€’å½’ã€‚
æˆ‘ä»¬é¦–å…ˆå¯¹é—­åŒ…ä¸­çš„é¡¹è¿›è¡Œæƒ…å†µåˆ†æã€‚è‹¥è¯¥é¡¹æ˜¯ä¸€ä¸ªå˜é‡æˆ–åº”ç”¨ï¼Œåˆ™ `ğ•`
ä¸ºå‡ï¼ˆ`Bot`ï¼‰ã€‚è‹¥è¯¥é¡¹æ˜¯ä¸€ä¸ª Î»-æŠ½è±¡ï¼Œåˆ™ `ğ•` å¯¹å€¼è¿›è¡Œé€’å½’ï¼Œå¦‚ä¸‹æ‰€è¿°ï¼š

```agda
ğ• v (clos (` xâ‚) Î³) = Bot
ğ• v (clos (M Â· Mâ‚) Î³) = Bot
ğ• âŠ¥ (clos (Æ› M) Î³) = âŠ¤
ğ• (v â†¦ w) (clos (Æ› N) Î³) =
    (âˆ€{c : Clos} â†’ ğ”¼ v c â†’ above-fun w â†’ Î£[ c' âˆˆ Clos ]
        (Î³ ,' c) âŠ¢ N â‡“ c'  Ã—  ğ• w c')
ğ• (u âŠ” v) (clos (Æ› N) Î³) = ğ• u (clos (Æ› N) Î³) Ã— ğ• v (clos (Æ› N) Î³)
```

<!--
* If the value is `âŠ¥`, then the result is true (`âŠ¤`).

* If the value is a join (u âŠ” v), then the result is the pair
  (conjunction) of ğ• is true for both u and v.

* The important case is for a function value `v â†¦ w` and closure
  `clos (Æ› N) Î³`. Given any closure `c` such that `ğ”¼ v c`, if `w` is
  greater than a function, then `N` evaluates (with `Î³` extended with `c`)
  to some closure `c'` and we have `ğ• w c'`.
-->

* è‹¥å€¼ä¸º `âŠ¥`ï¼Œåˆ™ç»“æœä¸ºçœŸï¼ˆ`âŠ¤`ï¼‰ã€‚

* è‹¥å€¼æ˜¯ä¸€ä¸ªè¿æ¥ï¼ˆ`u âŠ” v`ï¼‰ï¼Œåˆ™ç»“æœæ˜¯ä¸€ä¸ª `ğ•` ä¸ºçœŸæ—¶ `u` å’Œ `v` çš„åºå¯¹ï¼ˆåˆå–ï¼‰ã€‚

* æœ€å…³é”®çš„æƒ…å†µæ˜¯å‡½æ•°å€¼ `v â†¦ w` å’Œé—­åŒ… `clos (Æ› N) Î³`ã€‚ç»™å®šä»»æ„é—­åŒ… `c`
  ä½¿å¾— `ğ”¼ v c`ï¼Œè‹¥ `w` å¤§äºä¸€ä¸ªå‡½æ•°ï¼Œåˆ™ `N` æ±‚å€¼ï¼ˆç”¨ `c` æ‰©å±• `Î³`ï¼‰
  ä¸ºæŸä¸ªé—­åŒ… `c'`ï¼Œäºæ˜¯æˆ‘ä»¬æœ‰ `ğ• w c'`ã€‚

<!--
The definition of `ğ”¼` is straightforward. If `v` is a greater than a
function, then `M` evaluates to a closure related to `v`.
-->

`ğ”¼` çš„å®šä¹‰éå¸¸ç›´ç™½ï¼šè‹¥ `v` å¤§äºä¸€ä¸ªå‡½æ•°ï¼Œåˆ™ `M` æ±‚å€¼ä¸ºä¸€ä¸ªä¸ `v` å…³è”çš„é—­åŒ…ã€‚

```agda
ğ”¼ v (clos M Î³') = above-fun v â†’ Î£[ c âˆˆ Clos ] Î³' âŠ¢ M â‡“ c Ã— ğ• v c
```

<!--
The proof of the main lemma is by induction on `Î³ âŠ¢ M â†“ v`, so it goes
underneath lambda abstractions and must therefore reason about open
terms (terms with variables). So we must relate environments of
semantic values to environments of closures.  In the following, `ğ”¾`
relates `Î³` to `Î³'` if the corresponding values and closures are related
by `ğ”¼`.
-->

ä¸»å¼•ç†é€šè¿‡å¯¹ `Î³ âŠ¢ M â†“ v` è¿›è¡Œå½’çº³æ¥è¯æ˜ï¼Œæ‰€ä»¥å®ƒå±äº Î»-æŠ½è±¡çš„æƒ…å†µï¼Œ
å› è€Œå¿…é¡»å¯¹å¼€é¡¹ï¼ˆå³å¸¦å˜é‡çš„é¡¹ï¼‰è¿›è¡Œè®ºè¯ã€‚
å› æ­¤ï¼Œæˆ‘ä»¬å¿…é¡»å°†è¯­ä¹‰å€¼çš„ç¯å¢ƒä¸é—­åŒ…çš„ç¯å¢ƒå…³è”èµ·æ¥ã€‚
åœ¨ä¸‹æ–‡ä¸­ï¼Œå¦‚æœå¯¹åº”çš„å€¼å’Œé—­åŒ…é€šè¿‡ `ğ”¼` ç›¸å…³è”ï¼Œåˆ™ `ğ”¾` å°† `Î³` ä¸ `Î³'` ç›¸å…³è”ã€‚

```agda
ğ”¾ : âˆ€{Î“} â†’ Env Î“ â†’ ClosEnv Î“ â†’ Set
ğ”¾ {Î“} Î³ Î³' = âˆ€{x : Î“ âˆ‹ â˜…} â†’ ğ”¼ (Î³ x) (Î³' x)

ğ”¾-âˆ… : ğ”¾ `âˆ… âˆ…'
ğ”¾-âˆ… {()}

ğ”¾-ext : âˆ€{Î“}{Î³ : Env Î“}{Î³' : ClosEnv Î“}{v c}
      â†’ ğ”¾ Î³ Î³' â†’ ğ”¼ v c â†’ ğ”¾ (Î³ `, v) (Î³' ,' c)
ğ”¾-ext {Î“} {Î³} {Î³'} g e {Z} = e
ğ”¾-ext {Î“} {Î³} {Î³'} g e {S x} = g
```

<!--
We need a few properties of the `ğ•` and `ğ”¼` relations.  The first is that
a closure in the `ğ•` relation must be in weak-head normal form.  We
define WHNF has follows.
-->

æˆ‘ä»¬éœ€è¦ä¸€äº›å…³ç³» `ğ•` å’Œ `ğ”¼` çš„ç›¸å…³æ€§è´¨ã€‚é¦–å…ˆ `ğ•` å…³ç³»ä¸­çš„é—­åŒ…å¿…é¡»æ˜¯å¼±å¤´èŒƒå¼ï¼ˆWHNFï¼‰ã€‚
æˆ‘ä»¬å°† WHNF å®šä¹‰å¦‚ä¸‹ï¼š

```agda
data WHNF : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ Set where
  Æ›_ : âˆ€ {Î“} {N : Î“ , â˜… âŠ¢ â˜…}
     â†’ WHNF (Æ› N)
```

<!--
The proof goes by cases on the term in the closure.
-->

å¯é€šè¿‡å¯¹é—­åŒ…ä¸­çš„é¡¹è¿›è¡Œæƒ…å†µåˆ†æè¯æ˜ï¼š

```agda
ğ•â†’WHNF : âˆ€{Î“}{Î³ : ClosEnv Î“}{M : Î“ âŠ¢ â˜…}{v}
       â†’ ğ• v (clos M Î³) â†’ WHNF M
ğ•â†’WHNF {M = ` x} {v} ()
ğ•â†’WHNF {M = Æ› N} {v} vc = Æ›_
ğ•â†’WHNF {M = L Â· M} {v} ()
```

<!--
Next we have an introduction rule for `ğ•` that mimics the `âŠ”-intro`
rule. If both `u` and `v` are related to a closure `c`, then their join is
too.
-->

æ¥ç€æˆ‘ä»¬æœ‰ä¸€æ¡ `ğ•` çš„å¼•å…¥è§„åˆ™ï¼Œå®ƒç±»ä¼¼äº `âŠ”-intro` è§„åˆ™ã€‚
è‹¥ `u` å’Œ `v` éƒ½ä¸é—­åŒ… `c` ç›¸å…³è”ï¼Œåˆ™å®ƒä»¬çš„è¿æ¥ä¹Ÿä¸ `c` ç›¸å…³è”ï¼š

```agda
ğ•âŠ”-intro : âˆ€{c u v}
         â†’ ğ• u c â†’ ğ• v c
           ---------------
         â†’ ğ• (u âŠ” v) c
ğ•âŠ”-intro {clos (` x) Î³} () vc
ğ•âŠ”-intro {clos (Æ› N) Î³} uc vc = âŸ¨ uc , vc âŸ©
ğ•âŠ”-intro {clos (L Â· M) Î³} () vc
```

<!--
In a moment we prove that `ğ•` is preserved when going from a greater
value to a lesser value: if `ğ• v c` and `v' âŠ‘ v`, then `ğ• v' c`.
This property, named `sub-ğ•`, is needed by the main lemma in
the case for the `sub` rule.
-->

In a moment we prove that `ğ•` is preserved when going from a greater
value to a lesser value: if `ğ• v c` and `v' âŠ‘ v`, then `ğ• v' c`.
This property, named `sub-ğ•`, is needed by the main lemma in
the case for the `sub` rule.
ç¨åæˆ‘ä»¬è¯æ˜å½“ä»è¾ƒå¤§å€¼æ˜ å°„åˆ°è¾ƒå°å€¼æ—¶ï¼Œ`ğ•` ä¿æŒæˆç«‹ï¼šè‹¥ `ğ• v c` ä¸”
`v' âŠ‘ v` åˆ™ `ğ• v' c`ã€‚æˆ‘ä»¬å°†æ­¤æ€§è´¨å‘½åä¸º `sub-ğ•`ï¼Œå®ƒä¼šåœ¨ `sub`
è§„åˆ™çš„æƒ…å†µçš„ä¸»å¼•ç†ä¸­ç”¨åˆ°ã€‚

<!--
To prove `sub-ğ•`, we in turn need the following property concerning
values that are not greater than a function, that is, values that are
equivalent to `âŠ¥`. In such cases, `ğ• v (clos (Æ› N) Î³')` is trivially true.
-->

ä¸ºäº†è¯æ˜ `sub-ğ•`ï¼Œæˆ‘ä»¬è¿˜éœ€è¦ä»¥ä¸‹å…³äºã€Œä¸å¤§äºå‡½æ•°çš„å€¼ã€ï¼Œä¹Ÿå°±æ˜¯ç­‰ä»·äº
`âŠ¥` çš„å€¼çš„å±æ€§ã€‚åœ¨æ­¤æƒ…å†µä¸‹ï¼Œ`ğ• v (clos (Æ› N) Î³')` å¹³å‡¡æˆç«‹ã€‚

```agda
not-above-fun-ğ• : âˆ€{v : Value}{Î“}{Î³' : ClosEnv Î“}{N : Î“ , â˜… âŠ¢ â˜… }
    â†’ Â¬ above-fun v
      -------------------
    â†’ ğ• v (clos (Æ› N) Î³')
not-above-fun-ğ• {âŠ¥} af = tt
not-above-fun-ğ• {v â†¦ v'} af = contradiction âŸ¨ v , âŸ¨ v' , âŠ‘-refl âŸ© âŸ© af
not-above-fun-ğ• {vâ‚ âŠ” vâ‚‚} af
    with not-above-fun-âŠ”-inv af
... | âŸ¨ af1 , af2 âŸ© = âŸ¨ not-above-fun-ğ• af1 , not-above-fun-ğ• af2 âŸ©
```

The proofs of `sub-ğ•` and `sub-ğ”¼` are intertwined.

```agda
sub-ğ• : âˆ€{c : Clos}{v v'} â†’ ğ• v c â†’ v' âŠ‘ v â†’ ğ• v' c
sub-ğ”¼ : âˆ€{c : Clos}{v v'} â†’ ğ”¼ v c â†’ v' âŠ‘ v â†’ ğ”¼ v' c
```

<!--
We prove `sub-ğ•` by case analysis on the closure's term, to dispatch the
cases for variables and application. We then proceed by induction on
`v' âŠ‘ v`. We describe each case below.
-->

æˆ‘ä»¬é€šè¿‡å¯¹é—­åŒ…çš„é¡¹è¿›è¡Œæƒ…å†µåˆ†ææ¥è¯æ˜ `sub-ğ•`ï¼Œå³å°†æƒ…å†µåˆ†ä¸ºå˜é‡å’Œåº”ç”¨ä¸¤ç±»ï¼Œ
ç„¶åæˆ‘ä»¬å¯¹ `v' âŠ‘ v` è¿›è¡Œå½’çº³ã€‚æ¥ä¸‹æ¥è¯¦è¿°æ¯ä¸€ç§æƒ…å†µã€‚

```agda
sub-ğ• {clos (` x) Î³} {v} () lt
sub-ğ• {clos (L Â· M) Î³} () lt
sub-ğ• {clos (Æ› N) Î³} vc âŠ‘-bot = tt
sub-ğ• {clos (Æ› N) Î³} vc (âŠ‘-conj-L lt1 lt2) = âŸ¨ (sub-ğ• vc lt1) , sub-ğ• vc lt2 âŸ©
sub-ğ• {clos (Æ› N) Î³} âŸ¨ vv1 , vv2 âŸ© (âŠ‘-conj-R1 lt) = sub-ğ• vv1 lt
sub-ğ• {clos (Æ› N) Î³} âŸ¨ vv1 , vv2 âŸ© (âŠ‘-conj-R2 lt) = sub-ğ• vv2 lt
sub-ğ• {clos (Æ› N) Î³} vc (âŠ‘-trans{v = vâ‚‚} lt1 lt2) = sub-ğ• (sub-ğ• vc lt2) lt1
sub-ğ• {clos (Æ› N) Î³} vc (âŠ‘-fun lt1 lt2) ev1 sf
    with vc (sub-ğ”¼ ev1 lt1) (above-fun-âŠ‘ sf lt2)
... | âŸ¨ c , âŸ¨ Nc , v4 âŸ© âŸ© = âŸ¨ c , âŸ¨ Nc , sub-ğ• v4 lt2 âŸ© âŸ©
sub-ğ• {clos (Æ› N) Î³} {v â†¦ w âŠ” v â†¦ w'} âŸ¨ vcw , vcw' âŸ© âŠ‘-dist ev1c sf
    with above-fun? w | above-fun? w'
... | yes af2 | yes af3
    with vcw ev1c af2 | vcw' ev1c af3
... | âŸ¨ clos L Î´ , âŸ¨ Lâ‡“câ‚‚ , ğ•w âŸ© âŸ©
    | âŸ¨ câ‚ƒ , âŸ¨ Lâ‡“câ‚ƒ , ğ•w' âŸ© âŸ© rewrite â‡“-determ Lâ‡“câ‚ƒ Lâ‡“câ‚‚ with ğ•â†’WHNF ğ•w
... | Æ›_ =
      âŸ¨ clos L Î´ , âŸ¨ Lâ‡“câ‚‚ , âŸ¨ ğ•w , ğ•w' âŸ© âŸ© âŸ©
sub-ğ• {c} {v â†¦ w âŠ” v â†¦ w'} âŸ¨ vcw , vcw' âŸ©  âŠ‘-dist ev1c sf
    | yes af2 | no naf3
    with vcw ev1c af2
... | âŸ¨ clos {Î“'} L Î³â‚ , âŸ¨ Lâ‡“c2 , ğ•w âŸ© âŸ©
    with ğ•â†’WHNF ğ•w
... | Æ›_ {N = N'} =
      let ğ•w' = not-above-fun-ğ•{w'}{Î“'}{Î³â‚}{N'} naf3 in
      âŸ¨ clos (Æ› N') Î³â‚ , âŸ¨ Lâ‡“c2 , ğ•âŠ”-intro ğ•w ğ•w' âŸ© âŸ©
sub-ğ• {c} {v â†¦ w âŠ” v â†¦ w'} âŸ¨ vcw , vcw' âŸ© âŠ‘-dist ev1c sf
    | no naf2 | yes af3
    with vcw' ev1c af3
... | âŸ¨ clos {Î“'} L Î³â‚ , âŸ¨ Lâ‡“c3 , ğ•w'c âŸ© âŸ©
    with ğ•â†’WHNF ğ•w'c
... | Æ›_ {N = N'} =
      let ğ•wc = not-above-fun-ğ•{w}{Î“'}{Î³â‚}{N'} naf2 in
      âŸ¨ clos (Æ› N') Î³â‚ , âŸ¨ Lâ‡“c3 , ğ•âŠ”-intro ğ•wc ğ•w'c âŸ© âŸ©
sub-ğ• {c} {v â†¦ w âŠ” v â†¦ w'} âŸ¨ vcw , vcw' âŸ© âŠ‘-dist ev1c âŸ¨ v' , âŸ¨ w'' , lt âŸ© âŸ©
    | no naf2 | no naf3
    with above-fun-âŠ” âŸ¨ v' , âŸ¨ w'' , lt âŸ© âŸ©
... | injâ‚ af2 = contradiction af2 naf2
... | injâ‚‚ af3 = contradiction af3 naf3
```

<!--
* Case `âŠ‘-bot`. We immediately have `ğ• âŠ¥ (clos (Æ› N) Î³)`.
-->

* æƒ…å†µ `âŠ‘-bot`ï¼šæˆ‘ä»¬ç›´æ¥å°±æœ‰ `ğ• âŠ¥ (clos (Æ› N) Î³)`ã€‚

<!--
* Case `âŠ‘-conj-L`.
-->

* æƒ…å†µ `âŠ‘-conj-L`ï¼š

        vâ‚' âŠ‘ v     vâ‚‚' âŠ‘ v
        -------------------
        (vâ‚' âŠ” vâ‚‚') âŠ‘ v

<!--
  The induction hypotheses gives us `ğ• vâ‚' (clos (Æ› N) Î³)`
  and `ğ• vâ‚‚' (clos (Æ› N) Î³)`, which is all we need for this case.
-->

  å½’çº³æ³•åˆ™ç»™å‡ºäº† `ğ• vâ‚' (clos (Æ› N) Î³)` å’Œ `ğ• vâ‚‚' (clos (Æ› N) Î³)`ï¼Œ
  è¿™å°±æ˜¯æœ¬æƒ…å†µä¸­æ‰€æœ‰éœ€è¦çš„ä¸œè¥¿ã€‚

<!--
* Case `âŠ‘-conj-R1`.
-->

* æƒ…å†µ `âŠ‘-conj-R1`ï¼š

        v' âŠ‘ vâ‚
        -------------
        v' âŠ‘ (vâ‚ âŠ” vâ‚‚)

<!--
  The induction hypothesis gives us `ğ• v' (clos (Æ› N) Î³)`.
-->

  å½’çº³æ³•åˆ™ç»™å‡ºäº† `ğ• v' (clos (Æ› N) Î³)`ã€‚

<!--
* Case `âŠ‘-conj-R2`.
-->

* æƒ…å†µ `âŠ‘-conj-R2`ï¼š

        v' âŠ‘ vâ‚‚
        -------------
        v' âŠ‘ (vâ‚ âŠ” vâ‚‚)

<!--
  Again, the induction hypothesis gives us `ğ• v' (clos (Æ› N) Î³)`.
-->

  åŒæ ·ï¼Œå½’çº³æ³•åˆ™ç»™å‡ºäº† `ğ• v' (clos (Æ› N) Î³)`ã€‚

<!--
* Case `âŠ‘-trans`.
-->

* æƒ…å†µ `âŠ‘-trans`ï¼š

        v' âŠ‘ vâ‚‚   vâ‚‚ âŠ‘ v
        -----------------
             v' âŠ‘ v

<!--
  The induction hypothesis for `vâ‚‚ âŠ‘ v` gives us
  `ğ• vâ‚‚ (clos (Æ› N) Î³)`. We apply the induction hypothesis
  for `v' âŠ‘ vâ‚‚` to conclude that `ğ• v' (clos (Æ› N) Î³)`.
-->

  å½’çº³æ³•åˆ™ `vâ‚‚ âŠ‘ v` ç»™å‡ºäº† `ğ• vâ‚‚ (clos (Æ› N) Î³)`ã€‚æˆ‘ä»¬åº”ç”¨å½’çº³æ³•åˆ™
  `v' âŠ‘ vâ‚‚` å¯å¾— `ğ• v' (clos (Æ› N) Î³)`ã€‚

<!--
* Case `âŠ‘-dist`. This case  is the most difficult. We have
-->

* æƒ…å†µ `âŠ‘-dist`ï¼šè¿™ç§æƒ…å†µæ˜¯æœ€å›°éš¾çš„ã€‚æˆ‘ä»¬æœ‰

        ğ• (v â†¦ w) (clos (Æ› N) Î³)
        ğ• (v â†¦ w') (clos (Æ› N) Î³)

  éœ€è¦è¯æ˜

        ğ• (v â†¦ (w âŠ” w')) (clos (Æ› N) Î³)

<!--
  Let `c` be an arbitrary closure such that `ğ”¼ v c`.
  Assume `w âŠ” w'` is greater than a function.
  Unfortunately, this does not mean that both `w` and `w'`
  are above functions. But thanks to the lemma `above-fun-âŠ”`,
  we know that at least one of them is greater than a function.
-->

  ä»¤ `c` ä¸ºä»»æ„é—­åŒ…ä½¿å¾— `ğ”¼ v c`ã€‚å‡è®¾ `w âŠ” w'` å¤§äºä¸€ä¸ªå‡½æ•°ã€‚
  ä¸å¹¸çš„æ˜¯ï¼Œè¿™å¹¶ä¸æ„å‘³ç€ `w` å’Œ `w'` éƒ½å¤§äºè¯¥å‡½æ•°ã€‚ä½†å¹¸äºæœ‰å¼•ç†
  `above-fun-âŠ”`ï¼Œæˆ‘ä»¬çŸ¥é“å®ƒä»¬ä¸­è‡³å°‘æœ‰ä¸€ä¸ªå¤§äºè¯¥å‡½æ•°ã€‚

<!--
  * Suppose both of them are greater than a function.  Then we have
    `Î³ âŠ¢ N â‡“ clos L Î´` and `ğ• w (clos L Î´)`.  We also have `Î³ âŠ¢ N â‡“ câ‚ƒ` and
    `ğ• w' câ‚ƒ`.  Because the big-step semantics is deterministic, we have
    `câ‚ƒ â‰¡ clos L Î´`. Also, from `ğ• w (clos L Î´)` we know that `L â‰¡ Æ› N'`
    for some `N'`. We conclude that `ğ• (w âŠ” w') (clos (Æ› N') Î´)`.

  * Suppose one of them is greater than a function and the other is
    not: say `above-fun w` and `Â¬ above-fun w'`. Then from
    `ğ• (v â†¦ w) (clos (Æ› N) Î³)`
    we have `Î³ âŠ¢ N â‡“ clos L Î³â‚` and `ğ• w (clos L Î³â‚)`. From this we have
    `L â‰¡ Æ› N'` for some `N'`. Meanwhile, from `Â¬ above-fun w'` we have
    `ğ• w' (clos L Î³â‚)`. We conclude that
    `ğ• (w âŠ” w') (clos (Æ› N') Î³â‚)`.
-->

  * å‡è®¾ä»–ä»¬éƒ½å¤§äºè¯¥å‡½æ•°ï¼Œåˆ™æˆ‘ä»¬æœ‰ `Î³ âŠ¢ N â‡“ clos L Î´` å’Œ `ğ• w (clos L Î´)`ã€‚
    æˆ‘ä»¬è¿˜æœ‰ `Î³ âŠ¢ N â‡“ câ‚ƒ` å’Œ `ğ• w' câ‚ƒ`ã€‚ç”±äºå¤§æ­¥è¯­ä¹‰æ˜¯å¯åˆ¤å®šçš„ï¼Œäºæ˜¯æˆ‘ä»¬æœ‰
    `câ‚ƒ â‰¡ clos L Î´`ã€‚æ­¤å¤–ï¼Œæ ¹æ® `ğ• w (clos L Î´)` æˆ‘ä»¬çŸ¥é“å¯¹äºæŸä¸ª `N'`
    æœ‰ `L â‰¡ Æ› N'`ï¼Œäºæ˜¯å¯å¾— `ğ• (w âŠ” w') (clos (Æ› N') Î´)`ã€‚

  * å‡è®¾å…¶ä¸­ä¹‹ä¸€å¤§äºè¯¥å‡½æ•°è€Œå¦ä¸€ä¸ªä¸å¤§äºï¼šå³ `above-fun w` ä¸” `Â¬ above-fun w'`ã€‚
    é‚£ä¹ˆæ ¹æ® `ğ• (v â†¦ w) (clos (Æ› N) Î³)` æˆ‘ä»¬æœ‰ `Î³ âŠ¢ N â‡“ clos L Î³â‚` å’Œ
    `ğ• w (clos L Î³â‚)`ã€‚æ®æ­¤æˆ‘ä»¬æœ‰å¯¹äº `N'` æ¥è¯´ `L â‰¡ Æ› N'`ã€‚
    åŒæ—¶ï¼Œæ ¹æ® `Â¬ above-fun w'` æˆ‘ä»¬æœ‰ `ğ• w' (clos L Î³â‚)`ã€‚äºæ˜¯æˆ‘ä»¬å¯å¾—
    `ğ• (w âŠ” w') (clos (Æ› N') Î³â‚)`ã€‚


<!--
The proof of `sub-ğ”¼` is direct and explained below.
-->

`sub-ğ”¼` çš„è¯æ˜å¾ˆç›´æ¥ï¼Œå¦‚ä¸‹æ‰€è¿°ï¼š

```agda
sub-ğ”¼ {clos M Î³} {v} {v'} ğ”¼v v'âŠ‘v fv'
    with ğ”¼v (above-fun-âŠ‘ fv' v'âŠ‘v)
... | âŸ¨ c , âŸ¨ Mâ‡“c , ğ•v âŸ© âŸ© =
      âŸ¨ c , âŸ¨ Mâ‡“c , sub-ğ• ğ•v v'âŠ‘v âŸ© âŸ©
```

<!--
From `above-fun v'` and `v' âŠ‘ v` we have `above-fun v`.  Then with `ğ”¼ v c` we
obtain a closure `c` such that `Î³ âŠ¢ M â‡“ c` and `ğ• v c`. We conclude with an
application of `sub-ğ•` with `v' âŠ‘ v` to show `ğ• v' c`.
-->

æ ¹æ® `above-fun v'` å’Œ `v' âŠ‘ v` æˆ‘ä»¬æœ‰ `above-fun v`ã€‚
ç„¶åé€šè¿‡ `ğ”¼ v c` æˆ‘ä»¬å¾—åˆ°ä¸€ä¸ªé—­åŒ… `c`ï¼Œä½¿å¾— `Î³ âŠ¢ M â‡“ c` ä¸” `ğ• v c`ã€‚
æœ€åï¼Œæˆ‘ä»¬åº”ç”¨ `sub-ğ•` å’Œ `v' âŠ‘ v` å¯è¯æ˜ `ğ• v' c`ã€‚


<!--
## Programs with function denotation terminate via call-by-name
-->

## é€šè¿‡ä¼ åè°ƒç”¨çš„å¸¦å‡½æ•°æŒ‡ç§°çš„ç¨‹åºå¯åœæœº

The main lemma proves that if a term has a denotation that is above a
function, then it terminates via call-by-name. More formally, if
`Î³ âŠ¢ M â†“ v` and `ğ”¾ Î³ Î³'`, then `ğ”¼ v (clos M Î³')`. The proof is by
induction on the derivation of `Î³ âŠ¢ M â†“ v` we discuss each case below.

The following lemma, kth-x, is used in the case for the `var` rule.

```agda
kth-x : âˆ€{Î“}{Î³' : ClosEnv Î“}{x : Î“ âˆ‹ â˜…}
     â†’ Î£[ Î” âˆˆ Context ] Î£[ Î´ âˆˆ ClosEnv Î” ] Î£[ M âˆˆ Î” âŠ¢ â˜… ]
                 Î³' x â‰¡ clos M Î´
kth-x{Î³' = Î³'}{x = x} with Î³' x
... | clos{Î“ = Î”} M Î´ = âŸ¨ Î” , âŸ¨ Î´ , âŸ¨ M , refl âŸ© âŸ© âŸ©
```

```agda
â†“â†’ğ”¼ : âˆ€{Î“}{Î³ : Env Î“}{Î³' : ClosEnv Î“}{M : Î“ âŠ¢ â˜… }{v}
            â†’ ğ”¾ Î³ Î³' â†’ Î³ âŠ¢ M â†“ v â†’ ğ”¼ v (clos M Î³')
â†“â†’ğ”¼ {Î“} {Î³} {Î³'} ğ”¾Î³Î³' (var{x = x}) fÎ³x
    with kth-x{Î“}{Î³'}{x} | ğ”¾Î³Î³'{x = x}
... | âŸ¨ Î” , âŸ¨ Î´ , âŸ¨ M' , eq âŸ© âŸ© âŸ© | ğ”¾Î³Î³'x rewrite eq
    with ğ”¾Î³Î³'x fÎ³x
... | âŸ¨ c , âŸ¨ M'â‡“c , ğ•Î³x âŸ© âŸ© =
      âŸ¨ c , âŸ¨ (â‡“-var eq M'â‡“c) , ğ•Î³x âŸ© âŸ©
â†“â†’ğ”¼ {Î“} {Î³} {Î³'} ğ”¾Î³Î³' (â†¦-elim{L = L}{M = M}{v = vâ‚}{w = v} dâ‚ dâ‚‚) fv
    with â†“â†’ğ”¼ ğ”¾Î³Î³' dâ‚ âŸ¨ vâ‚ , âŸ¨ v , âŠ‘-refl âŸ© âŸ©
... | âŸ¨ clos L' Î´ , âŸ¨ Lâ‡“L' , ğ•vâ‚â†¦v âŸ© âŸ©
    with ğ•â†’WHNF ğ•vâ‚â†¦v
... | Æ›_ {N = N}
    with ğ•vâ‚â†¦v {clos M Î³'} (â†“â†’ğ”¼ ğ”¾Î³Î³' dâ‚‚) fv
... | âŸ¨ c' , âŸ¨ Nâ‡“c' , ğ•v âŸ© âŸ© =
    âŸ¨ c' , âŸ¨ â‡“-app Lâ‡“L' Nâ‡“c' , ğ•v âŸ© âŸ©
â†“â†’ğ”¼ {Î“} {Î³} {Î³'} ğ”¾Î³Î³' (â†¦-intro{N = N}{v = v}{w = w} d) fvâ†¦w =
    âŸ¨ clos (Æ› N) Î³' , âŸ¨ â‡“-lam , E âŸ© âŸ©
    where E : {c : Clos} â†’ ğ”¼ v c â†’ above-fun w
            â†’ Î£[ c' âˆˆ Clos ] (Î³' ,' c) âŠ¢ N â‡“ c'  Ã—  ğ• w c'
          E {c} ğ”¼vc fw = â†“â†’ğ”¼ (Î» {x} â†’ ğ”¾-ext{Î“}{Î³}{Î³'} ğ”¾Î³Î³' ğ”¼vc {x}) d fw
â†“â†’ğ”¼ ğ”¾Î³Î³' âŠ¥-intro fâŠ¥ = âŠ¥-elim (above-funâŠ¥ fâŠ¥)
â†“â†’ğ”¼ ğ”¾Î³Î³' (âŠ”-intro{v = vâ‚}{w = vâ‚‚} dâ‚ dâ‚‚) fv12
    with above-fun? vâ‚ | above-fun? vâ‚‚
... | yes fv1 | yes fv2
    with â†“â†’ğ”¼ ğ”¾Î³Î³' dâ‚ fv1 | â†“â†’ğ”¼ ğ”¾Î³Î³' dâ‚‚ fv2
... | âŸ¨ câ‚ , âŸ¨ Mâ‡“câ‚ , ğ•vâ‚ âŸ© âŸ© | âŸ¨ câ‚‚ , âŸ¨ Mâ‡“câ‚‚ , ğ•vâ‚‚ âŸ© âŸ©
    rewrite â‡“-determ Mâ‡“câ‚‚ Mâ‡“câ‚ =
    âŸ¨ câ‚ , âŸ¨ Mâ‡“câ‚ , ğ•âŠ”-intro ğ•vâ‚ ğ•vâ‚‚ âŸ© âŸ©
â†“â†’ğ”¼ ğ”¾Î³Î³' (âŠ”-intro{v = vâ‚}{w = vâ‚‚} dâ‚ dâ‚‚) fv12 | yes fv1 | no nfv2
    with â†“â†’ğ”¼ ğ”¾Î³Î³' dâ‚ fv1
... | âŸ¨ clos {Î“'} M' Î³â‚ , âŸ¨ Mâ‡“câ‚ , ğ•vâ‚ âŸ© âŸ©
    with ğ•â†’WHNF ğ•vâ‚
... | Æ›_ {N = N} =
    let ğ•vâ‚‚ = not-above-fun-ğ•{vâ‚‚}{Î“'}{Î³â‚}{N} nfv2 in
    âŸ¨ clos (Æ› N) Î³â‚ , âŸ¨ Mâ‡“câ‚ , ğ•âŠ”-intro ğ•vâ‚ ğ•vâ‚‚ âŸ© âŸ©
â†“â†’ğ”¼ ğ”¾Î³Î³' (âŠ”-intro{v = vâ‚}{w = vâ‚‚} dâ‚ dâ‚‚) fv12 | no nfv1  | yes fv2
    with â†“â†’ğ”¼ ğ”¾Î³Î³' dâ‚‚ fv2
... | âŸ¨ clos {Î“'} M' Î³â‚ , âŸ¨ M'â‡“câ‚‚ , ğ•2c âŸ© âŸ©
    with ğ•â†’WHNF ğ•2c
... | Æ›_ {N = N} =
    let ğ•1c = not-above-fun-ğ•{vâ‚}{Î“'}{Î³â‚}{N} nfv1 in
    âŸ¨ clos (Æ› N) Î³â‚ , âŸ¨ M'â‡“câ‚‚ , ğ•âŠ”-intro ğ•1c ğ•2c âŸ© âŸ©
â†“â†’ğ”¼ ğ”¾Î³Î³' (âŠ”-intro dâ‚ dâ‚‚) fv12 | no nfv1  | no nfv2
    with above-fun-âŠ” fv12
... | injâ‚ fv1 = contradiction fv1 nfv1
... | injâ‚‚ fv2 = contradiction fv2 nfv2
â†“â†’ğ”¼ {Î“} {Î³} {Î³'} {M} {v'} ğ”¾Î³Î³' (sub{v = v} d v'âŠ‘v) fv'
    with â†“â†’ğ”¼ {Î“} {Î³} {Î³'} {M} ğ”¾Î³Î³' d (above-fun-âŠ‘ fv' v'âŠ‘v)
... | âŸ¨ c , âŸ¨ Mâ‡“c , ğ•v âŸ© âŸ© =
      âŸ¨ c , âŸ¨ Mâ‡“c , sub-ğ• ğ•v v'âŠ‘v âŸ© âŸ©
```

* Case `var`. Looking up `x` in `Î³'` yields some closure, `clos M' Î´`,
  and from `ğ”¾ Î³ Î³'` we have `ğ”¼ (Î³ x) (clos M' Î´)`. With the premise
  `above-fun (Î³ x)`, we obtain a closure `c` such that `Î´ âŠ¢ M' â‡“ c`
  and `ğ• (Î³ x) c`. To conclude `Î³' âŠ¢ x â‡“ c` via `â‡“-var`, we
  need `Î³' x â‰¡ clos M' Î´`, which is obvious, but it requires some
  Agda shananigans via the `kth-x` lemma to get our hands on it.

* Case `â†¦-elim`. We have `Î³ âŠ¢ L Â· M â†“ v`.
  The induction hypothesis for `Î³ âŠ¢ L â†“ vâ‚ â†¦ v`
  gives us `Î³' âŠ¢ L â‡“ clos L' Î´` and `ğ• v (clos L' Î´)`.
  Of course, `L' â‰¡ Æ› N` for some `N`.
  By the induction hypothesis for `Î³ âŠ¢ M â†“ vâ‚`,
  we have `ğ”¼ vâ‚ (clos M Î³')`.
  Together with the premise `above-fun v` and `ğ• v (clos L' Î´)`,
  we obtain a closure `c'` such that `Î´ âŠ¢ N â‡“ c'` and `ğ• v c'`.
  We conclude that `Î³' âŠ¢ L Â· M â‡“ c'` by rule `â‡“-app`.

* Case `â†¦-intro`. We have `Î³ âŠ¢ Æ› N â†“ v â†¦ w`.
  We immediately have `Î³' âŠ¢ Æ› M â‡“ clos (Æ› M) Î³'` by rule `â‡“-lam`.
  But we also need to prove `ğ• (v â†¦ w) (clos (Æ› N) Î³')`.
  Let `c` by an arbitrary closure such that `ğ”¼ v c`.
  Suppose `v'` is greater than a function value.
  We need to show that `Î³' , c âŠ¢ N â‡“ c'` and `ğ• v' c'` for some `c'`.
  We prove this by the induction hypothesis for `Î³ , v âŠ¢ N â†“ v'`
  but we must first show that `ğ”¾ (Î³ , v) (Î³' , c)`. We prove
  that by the lemma `ğ”¾-ext`, using facts `ğ”¾ Î³ Î³'` and `ğ”¼ v c`.

* Case `âŠ¥-intro`. We have the premise `above-fun âŠ¥`, but that's impossible.

* Case `âŠ”-intro`. We have `Î³ âŠ¢ M â†“ (vâ‚ âŠ” vâ‚‚)` and `above-fun (vâ‚ âŠ” vâ‚‚)`
  and need to show `Î³' âŠ¢ M â†“ c` and `ğ• (vâ‚ âŠ” vâ‚‚) c` for some `c`.
  Again, by `above-fun-âŠ”`, at least one of `vâ‚` or `vâ‚‚` is greater than
  a function.

  * Suppose both `vâ‚` and `vâ‚‚` are greater than a function value.
    By the induction hypotheses for `Î³ âŠ¢ M â†“ vâ‚` and `Î³ âŠ¢ M â†“ vâ‚‚`
    we have `Î³' âŠ¢ M â‡“ câ‚`, `ğ• vâ‚ câ‚`, `Î³' âŠ¢ M â‡“ câ‚‚`, and `ğ• vâ‚‚ câ‚‚`
    for some `câ‚` and `câ‚‚`. Because `â‡“` is deterministic, we have `câ‚‚ â‰¡ câ‚`.
    Then by `ğ•âŠ”-intro` we conclude that `ğ• (vâ‚ âŠ” vâ‚‚) câ‚`.

  * Without loss of generality, suppose `vâ‚` is greater than a function
    value but `vâ‚‚` is not.  By the induction hypotheses for `Î³ âŠ¢ M â†“ vâ‚`,
    and using `ğ•â†’WHNF`, we have `Î³' âŠ¢ M â‡“ clos (Æ› N) Î³â‚`
    and `ğ• vâ‚ (clos (Æ› N) Î³â‚)`.
    Then because `vâ‚‚` is not greater than a function, we also have
    `ğ• vâ‚‚ (clos (Æ› N) Î³â‚)`. We conclude that `ğ• (vâ‚ âŠ” vâ‚‚) (clos (Æ› N) Î³â‚)`.

* Case `sub`. We have `Î³ âŠ¢ M â†“ v`, `v' âŠ‘ v`, and `above-fun v'`.
  We need to show that `Î³' âŠ¢ M â‡“ c` and `ğ• v' c` for some `c`.
  We have `above-fun v` by `above-fun-âŠ‘`,
  so the induction hypothesis for `Î³ âŠ¢ M â†“ v` gives us a closure `c`
  such that `Î³' âŠ¢ M â‡“ c` and `ğ• v c`. We conclude that `ğ• v' c` by `sub-ğ•`.


## Proof of denotational adequacy

From the main lemma we can directly show that `â„° M â‰ƒ â„° (Æ› N)` implies
that `M` big-steps to a lambda, i.e., `âˆ… âŠ¢ M â‡“ clos (Æ› Nâ€²) Î³`.

```agda
â†“â†’â‡“ : âˆ€{M : âˆ… âŠ¢ â˜…}{N : âˆ… , â˜… âŠ¢ â˜…}  â†’  â„° M â‰ƒ â„° (Æ› N)
         â†’  Î£[ Î“ âˆˆ Context ] Î£[ Nâ€² âˆˆ (Î“ , â˜… âŠ¢ â˜…) ] Î£[ Î³ âˆˆ ClosEnv Î“ ]
            âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î³
â†“â†’â‡“{M}{N} eq
    with â†“â†’ğ”¼ ğ”¾-âˆ… ((projâ‚‚ (eq `âˆ… (âŠ¥ â†¦ âŠ¥))) (â†¦-intro âŠ¥-intro))
                 âŸ¨ âŠ¥ , âŸ¨ âŠ¥ , âŠ‘-refl âŸ© âŸ©
... | âŸ¨ clos {Î“} Mâ€² Î³ , âŸ¨ Mâ‡“c , Vc âŸ© âŸ©
    with ğ•â†’WHNF Vc
... | Æ›_ {N = Nâ€²} =
    âŸ¨ Î“ , âŸ¨ Nâ€² , âŸ¨ Î³ , Mâ‡“c âŸ©  âŸ© âŸ©
```

The proof goes as follows. We derive `âˆ… âŠ¢ Æ› N â†“ âŠ¥ â†¦ âŠ¥` and
then `â„° M â‰ƒ â„° (Æ› N)` gives us `âˆ… âŠ¢ M â†“ âŠ¥ â†¦ âŠ¥`. We conclude
by applying the main lemma to obtain `âˆ… âŠ¢ M â‡“ clos (Æ› Nâ€²) Î³`
for some `Nâ€²` and `Î³`.

Now to prove the adequacy property. We apply the above
lemma to obtain `âˆ… âŠ¢ M â‡“ clos (Æ› Nâ€²) Î³` and then
apply `cbnâ†’reduce` to conclude.

```
adequacy : âˆ€{M : âˆ… âŠ¢ â˜…}{N : âˆ… , â˜… âŠ¢ â˜…}
   â†’  â„° M â‰ƒ â„° (Æ› N)
   â†’ Î£[ Nâ€² âˆˆ (âˆ… , â˜… âŠ¢ â˜…) ]
     (M â€”â†  Æ› Nâ€²)
adequacy{M}{N} eq
    with â†“â†’â‡“ eq
... | âŸ¨ Î“ , âŸ¨ Nâ€² , âŸ¨ Î³ , Mâ‡“ âŸ© âŸ© âŸ© =
    cbnâ†’reduce Mâ‡“
```

## Call-by-name is equivalent to beta reduction

As promised, we return to the question of whether call-by-name
evaluation is equivalent to beta reduction. In chapter
[BigStep](/BigStep/) we established the forward
direction: that if call-by-name produces a result, then the program
beta reduces to a lambda abstraction (`cbnâ†’reduce`).  We now prove the backward
direction of the if-and-only-if, leveraging our results about the
denotational semantics.

```
reduceâ†’cbn : âˆ€ {M : âˆ… âŠ¢ â˜…} {N : âˆ… , â˜… âŠ¢ â˜…}
           â†’ M â€”â†  Æ› N
           â†’ Î£[ Î” âˆˆ Context ] Î£[ Nâ€² âˆˆ Î” , â˜… âŠ¢ â˜… ] Î£[ Î´ âˆˆ ClosEnv Î” ]
             âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î´
reduceâ†’cbn Mâ€”â† Æ›N = â†“â†’â‡“ (soundness Mâ€”â† Æ›N)
```

Suppose `M â€”â†  Æ› N`. Soundness of the denotational semantics gives us
`â„° M â‰ƒ â„° (Æ› N)`. Then by `â†“â†’â‡“` we conclude that
`âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î´` for some `Nâ€²` and `Î´`.

Putting the two directions of the if-and-only-if together, we
establish that call-by-name evaluation is equivalent to beta reduction
in the following sense.

```
cbnâ†”reduce : âˆ€ {M : âˆ… âŠ¢ â˜…}
           â†’ (Î£[ N âˆˆ âˆ… , â˜… âŠ¢ â˜… ] (M â€”â†  Æ› N))
             iff
             (Î£[ Î” âˆˆ Context ] Î£[ Nâ€² âˆˆ Î” , â˜… âŠ¢ â˜… ] Î£[ Î´ âˆˆ ClosEnv Î” ]
               âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î´)
cbnâ†”reduce {M} = âŸ¨ (Î» x â†’ reduceâ†’cbn (projâ‚‚ x)) ,
                   (Î» x â†’ cbnâ†’reduce (projâ‚‚ (projâ‚‚ (projâ‚‚ x)))) âŸ©
```


## Unicode

æœ¬ç« ä½¿ç”¨äº†ä»¥ä¸‹ Unicodeï¼š

    ğ”¼  U+1D53C  MATHEMATICAL DOUBLE-STRUCK CAPITAL E (\bE)
    ğ”¾  U+1D53E  MATHEMATICAL DOUBLE-STRUCK CAPITAL G (\bG)
    ğ•  U+1D53E  MATHEMATICAL DOUBLE-STRUCK CAPITAL V (\bV)
